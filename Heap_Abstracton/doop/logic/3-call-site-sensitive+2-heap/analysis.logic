/**
 * Context-sensitive pointer analysis with context of 3 call site and
 * a context-sensitive heap abstraction of 2 call site
 *
 * @author Tian Tan
 * @author Yue Li
 */

#include "macros.logic"
#include "../context-sensitive.logic"
#include "../library.logic"

// In this analysis, the real context is a pair of MethodInvocationRefs
// and the real heap context a single MethodInvocationRef. Keeping mapping 
// functions is the way to handle analyses where HContext = Context 
// (since the language considers them different types).
ContextFromRealContext[?invocation1, ?invocation2, ?invocation3] = ?ctx ->
  Context(?ctx), CallGraphEdgeSourceRef(?invocation1), CallGraphEdgeSourceRef(?invocation2), CallGraphEdgeSourceRef(?invocation3).
lang:skolem(`ContextFromRealContext).
RealContext1FromContext[?ctx] = ?invocation -> 
  Context(?ctx), CallGraphEdgeSourceRef(?invocation).
RealContext2FromContext[?ctx] = ?invocation -> 
  Context(?ctx), CallGraphEdgeSourceRef(?invocation).
RealContext3FromContext[?ctx] = ?invocation -> 
  Context(?ctx), CallGraphEdgeSourceRef(?invocation).

RealContext1FromContext[?ctx] = ?invocation1,
RealContext2FromContext[?ctx] = ?invocation2,
RealContext3FromContext[?ctx] = ?invocation3 <- 
  ContextFromRealContext[?invocation1, ?invocation2, ?invocation3] = ?ctx.

HContextFromRealHContext[?invocation1, ?invocation2] = ?hctx ->
  HContext(?hctx), CallGraphEdgeSourceRef(?invocation1), CallGraphEdgeSourceRef(?invocation2).
lang:skolem(`HContextFromRealHContext).
RealHContext1FromHContext[?hctx] = ?invocation ->
  HContext(?hctx), CallGraphEdgeSourceRef(?invocation).
RealHContext2FromHContext[?hctx] = ?invocation ->
  HContext(?hctx), CallGraphEdgeSourceRef(?invocation).
  
RealHContext1FromHContext[?hctx] = ?invocation1,
RealHContext2FromHContext[?hctx] = ?invocation2 <-
  HContextFromRealHContext[?invocation1, ?invocation2] = ?hctx.


// Creating special immutable method invocation constant
ImmutableInvValue[] = ?immInv <-
  MethodInvocationValue(?immInv, "<<immutable>>").


// Create initial objects with their heapcontexts.
HContextFromRealHContext[?invocation1, ?invocation2] = ?hctx,
HContext(?hctx),
SystemThreadGroup(?hctx, ?alloc) <-
  MySystemThreadGroup(?invocation1, ?invocation2, ?alloc).

HContextFromRealHContext[?invocation1, ?invocation2] = ?hctx,
HContext(?hctx),
MainThreadGroup(?hctx, ?alloc) <-
  MyMainThreadGroup(?invocation1, ?invocation2, ?alloc).

HContextFromRealHContext[?invocation1, ?invocation2] = ?hctx,
HContext(?hctx),
MainThread(?hctx, ?alloc) <-
  MyMainThread(?invocation1, ?invocation2, ?alloc).

StartupInvocation[] = ?startupInvocation <-
  MethodInvocationValue(?startupInvocation, "<<jvm-startup>>").

/**
 * Merge optimization hack
 */

// Better to do the join twice than to waste the space for a full MergeBasis
// relation for this analysis.
lang:derivationType[`MergeBasis] = "Derived".

// For this analysis, we only need two of the parameters that may 
// influence the new context object.
MyMergeBasis(?callerCtx, ?invocation) <-
  MergeBasis(_, _, ?callerCtx, ?invocation).

// We create new context objects sparingly, because of the high cost.
// We also cache them, so they can be looked up without a join.
Context(?calleeCtx),
ContextFromRealContext[RealContext2FromContext[?callerCtx],
                       RealContext3FromContext[?callerCtx],
                       ?invocation] = ?calleeCtx,
OptimizeMerge[?callerCtx, ?invocation] = ?calleeCtx <-
  MyMergeBasis(?callerCtx, ?invocation).


/**
 * Reachable
 */
ReachableContext(?ctx, ?method),
ContextFromRealContext[?invo, ?invo, ?invo] = ?ctx,
Context(?ctx)
 <-
  MainMethodDeclaration(?method),
  MethodInvocationValue(?invo, "<<initial-context>>").

ReachableContext(?ctx, ?method),
ContextFromRealContext[?invo, ?invo, ?invo] = ?ctx,
Context(?ctx)
 <-
  ImplicitReachable(?method),
  MethodInvocationValue(?invo, "<<jvm-startup>>").

ReachableContext(?ctx, ?clinit),
ContextFromRealContext[?invo, ?invo, ?invo] = ?ctx,
Context(?ctx)
 <-
  InitializedClass(?class),
  ClassInitializer[?class] = ?clinit,
  MethodInvocationValue(?invo, "<<class-initialization>>").
